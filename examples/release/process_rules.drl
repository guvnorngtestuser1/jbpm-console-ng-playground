package org.jbpm.examples;


import java.util.Map;
import java.util.HashMap;
import org.kie.runtime.process.ProcessInstance;
import org.jbpm.workflow.instance.impl.WorkflowProcessInstanceImpl;
import org.kie.event.process.*;
import org.jbpm.task.api.TaskServiceEntryPoint;
import org.jbpm.task.impl.factories.TaskFactory;
import java.io.StringReader;
import org.jbpm.task.Task;
import org.droolsjbpm.services.api.RulesNotificationService;

global RulesNotificationService rulesNotificationService;

global TaskServiceEntryPoint taskService;

declare ProcessStartedEvent
	@role(event)
end
declare ProcessCompletedEvent
	@role(event)
end

declare ProcessNodeTriggeredEvent
	@role(event)
end

declare ProcessNodeLeftEvent
	@role(event)
end

query getProcessInstances()
    $w: WorkflowProcessInstanceImpl( )
end

rule "Ongoing process for more than 15 seconds"
 when
	$w: WorkflowProcessInstanceImpl( )
	$pse: ProcessStartedEvent(processInstance == $w) from entry-point "process-events"
        not(ProcessCompletedEvent(processInstance == $w,  this after[0s, 15s] $pse ) from entry-point "process-events") 
 then
	System.out.println("The Process Instance (id="+$w.getId()+") is taking too long!");
	rulesNotificationService.insertNotification("The Process Instance (id="+$w.getId()+") is taking too long!");
end


rule "Warn Process Instance not finished after 35 seconds"
 when
	$w: WorkflowProcessInstanceImpl( )
	$pse: ProcessStartedEvent(processInstance == $w) from entry-point "process-events"
        not(ProcessCompletedEvent(processInstance == $w,  this after[0s, 35s] $pse ) from entry-point "process-events") 
 then
	System.out.println("WARN: The Process Instance (id="+$w.getId()+") will be aborted in 2 minutes!");
	rulesNotificationService.insertNotification("WARN: The Process Instance (id="+$w.getId()+") will be aborted in 2 minutes!");
end


rule "Warn Two Process Instance doing a Release on the same Directory"
 when
	$w1: WorkflowProcessInstanceImpl($releasePath1: variables['release_path'] )
	$w2: WorkflowProcessInstanceImpl( id > $w1.id  , variables['release_path'] == $releasePath1)
 then
	System.out.println("WARN: The Process Instance (id="+$w2.getId()+") is using the same Release Path than "+$w1.getId() +" -> "+$releasePath1);
	rulesNotificationService.insertNotification("WARN: The Process Instance (id="+$w2.getId()+") is using the same Release Path than "+$w1.getId()+" -> "+$releasePath1);
	rulesNotificationService.insertNotification("SEVERE: Aborting the process: "+$w2.getId());	
        kcontext.getKnowledgeRuntime().abortProcessInstance($w2.getId());
end

rule "More than two Fix Issues for the same Release Process Instance"
  when
	$w1: WorkflowProcessInstanceImpl($id: id)
	Number( intValue > 1) from accumulate($e: ProcessNodeTriggeredEvent(processInstance.id == $id, nodeInstance.nodeName == "Fix Issues") from entry-point "process-events", count($e))

  then
	System.out.println("WARN: The Process Instance (id="+$w1.getId()+") has being rejected at least 2 times");
	rulesNotificationService.insertNotification("WARN: The Process Instance (id="+$w1.getId()+") has being rejected at least 2 times");
end

rule "Trigger Test is taking too much time to run the tests - Create ad - hoc task"
  when
	$w1: WorkflowProcessInstanceImpl($id: id)
	$onEntry: ProcessNodeTriggeredEvent(processInstance.id == $id, nodeInstance.nodeName == "Trigger Tests") from entry-point "process-events"
	$onExit: ProcessNodeLeftEvent(this after[10s] $onEntry, processInstance.id == $id, nodeInstance.nodeName == "Trigger Tests") from entry-point "process-events"
  then
	rulesNotificationService.insertNotification("WARN: Trigger Tests activity from process id="+$w1.getId()+" is took too much time! an ad-hoc task was created to improve the performance of those tests");
	rulesNotificationService.insertNotification("WARN: Trigger Tests activity from process id="+$w1.getId()+" is took too much time! an ad-hoc task was created to improve the performance of those tests");
	String str = "(with (new Task()) { priority = 55, taskData = (with( new TaskData()) { } ), ";
        str += "peopleAssignments = (with ( new PeopleAssignments() ) { potentialOwners = [new Group('Release Manager')  ], }),";
        str += "names = [ new I18NText( 'en-UK', 'Improve Trigger Test Performance')] })";
	Task task = TaskFactory.evalTask(new StringReader(str));

        taskService.addTask(task, new HashMap<String, Object>());
	retract($onEntry);
	retract($onExit);
end

